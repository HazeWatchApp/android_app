buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "my.codeandroid.hazewatch"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 8
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        Properties props = new Properties()
        props.load(new FileInputStream("crashlytics.properties"))

        manifestPlaceholders = [
                fabricApiKey: props['fabricApiKey']
        ]
    }

    lintOptions {
        checkReleaseBuilds false
    }

    // see gradle.properties in the root of the project
    File propsFile = new File(project.property("keystore.properties.path") as String)
    Properties props = null;
    if (propsFile.exists()) {
        props = new Properties()
        props.load(new FileInputStream(propsFile))
    }

    signingConfigs {
        release {
            if (props) {
                storeFile file(props['storeFile'])
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (props) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }
}

ext {
    supportLibVersion = '27.0.2'
    gmsVersion = '11.8.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.google.android.gms:play-services-location:${gmsVersion}"
    implementation "com.google.android.gms:play-services-analytics:${gmsVersion}"

    // new dependencies
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0') {
        transitive = true;
    }

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}
