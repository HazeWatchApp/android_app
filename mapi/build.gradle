buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'android'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':mapi-pulltorefresh:library')
    compile('de.keyboardsurfer.android.widget:crouton:1.8.3') {
        // exclusion is not neccessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile 'com.android.support:support-v4:18.0.+'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    //the file is at gradle.properties in the root of the project
    File keystoreFile = file(project.property("keystore.dir"))

    Properties props = new Properties()
    props.load(new FileInputStream(keystoreFile))

    signingConfigs {
        release {
            storeFile file(props['storeFile'])
            keyAlias props['keyAlias']
            storePassword props['storePassword']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {

        debug {
        }

        release {
            debuggable false
            runProguard false
            signingConfig signingConfigs.release
            proguardFile 'proguard-project.txt'
        }
    }
}
