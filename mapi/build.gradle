buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "my.codeandroid.hazewatch"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 8
        versionName "2.0"

        Properties props = new Properties()
        props.load(new FileInputStream("crashlytics.properties"))

        manifestPlaceholders = [
                fabricApiKey: props['fabricApiKey']
        ]
    }

    lintOptions {
        checkReleaseBuilds false
    }

    // see gradle.properties in the root of the project
    File propsFile = new File(project.property("keystore.properties.path") as String)
    Properties props = null;
    if (propsFile.exists()) {
        props = new Properties()
        props.load(new FileInputStream(propsFile))
    }

    signingConfigs {
        release {
            if (props) {
                storeFile file(props['storeFile'])
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (props) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }
}

ext {
    supportLibVersion = '24.2.0'
    gmsVersion = '8.4.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:transition:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.google.android.gms:play-services-location:${gmsVersion}"
    compile "com.google.android.gms:play-services-analytics:${gmsVersion}"

    // new dependencies
    compile 'com.google.code.gson:gson:2.4'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5') {
        transitive = true;
    }

    apt 'com.jakewharton:butterknife-compiler:8.0.1'

}
